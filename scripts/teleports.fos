#ifndef TELEPORTS_MODULE
#define TELEPORTS_MODULE

#include "_utils.fos"
#include "teleports_h.fos"

import uint CountMapPlayers( Map@ map ) from "manager";

//~run teleports go 0 0 0
void go( Critter& cr, int p0, int p1, int p2 )
{
	ShowTeleports(cr);
}

void ShowTeleports( Critter& cr )
{
	InitTeleports();
	iMenuHandler@ handler = MenuTeleports( "", teleports, null );
	iDialogBox@ menu = OpenMenu( cr, "Select", handler );
}

class MenuTeleports: CenteredMenuHandler
{
	bool isRoot;
	bool redrawing;
	string fullName;
	Teleport@ teleport;
	MenuTeleports@ parent;
	
	MenuTeleports( string fullName, Teleport@ teleport, MenuTeleports@ parent ) 
	{
		this.redrawing = true;
		this.isRoot = !valid(parent);
		this.fullName = fullName;
		@this.teleport = @teleport;
		@this.parent = @parent;
	}

	bool MenuUpdate( Critter& cr, iDialogBox& menu )
	{
		if( !redrawing )
		{
			return false;
		}
		
		uint l = teleport.list();
		for( uint n = 0; n < l; n++ )
		{
			if( menu.ButtonMsg( STR_LEX_NAME , "$name" + teleport.online(n) + " " + teleport.name(n) ) )
			{ 
				Teleport@ target = teleport.get(n);
				if( target.list() == 0 )
				{
					target.warp( cr );
					stopMenu();
					return false;
				}
				else
				{
					MenuTeleports@ new_menu = MenuTeleports( this.name(), target, this );
					new_menu.isRoot = false;
					return menu.OpenChild( this.name(), new_menu );
				}
			}
		}		
		
		if( !isRoot )
		{
			if( menu.ButtonMsg( STR_PREVIOUS ) )
			{
				return false;
			}
		}
		
		if( menu.ButtonMsg( STR_CLOSE_MENU_BUTTON ) )
		{
			stopMenu();			
			return false;
		}

		return true;
	}
	
	void stopMenu()
	{
		redrawing = false;
		
		MenuTeleports@ loopback = @parent;
		while( valid( loopback ) )
		{
			loopback.redrawing = false;
			@loopback = @loopback.parent;
		}
	}

	int getDescriptionFile()
	{
		return TEXTMSG_TEXT;
	}

	int getDescriptionLine()
	{
		return STR_TELEPORTS_MENU_MAIN;
	}
	
	string name() {
		return this.fullName + " " + this.teleport.name();
	}
	
	string@ Description( Critter& cr )
	{
		string info = "$name" + name();		
		
		return info;
	}
	
	bool ShouldRedraw( Critter& cr )
	{
		return true;
	}	
}

Teleport@ teleports;

void InitTeleports()
{
	if( valid( teleports ) )
	{
		return;
	}
	
	@teleports = 
		Teleport( "Teleports" )
		.add( "Старт",						LOCATION_StartMap,		0, 337, 184 )
		.add( "Сон",						LOCATION_Dream,			0, 296, 178 )
		.add( "Рай",						LOCATION_Deadloc,		0, 129, 77  )
		.add( "Ад",							LOCATION_Hell,			0, 229, 177 )
		.add( "Арена 1",					LOCATION_TLJ_ARENA,		0, 440, 510 )
		.add( "Арена 2",					LOCATION_TLJ_ARENA,		1, 180, 270 )
		.add( Teleport( "Модок" ) 
			.add( "Шахта вход", 		LOCATION_Modoc_1_1,		0, 157, 144 )
			.add( "Шахта штольни", 		LOCATION_Modoc_1_1,		1, 193, 214 )
			.add( "Гетто", 				LOCATION_Modoc_2_1,		0, 155, 200 )
			.add( "Модок", 				LOCATION_Modoc_city,	0, 431, 373 )
			.add( "Этажи", 				LOCATION_Modoc_city,	1, 463, 378 )
			.add( "Подвал",				LOCATION_Modoc_city,	2, 430, 321 )
			.add( "Ферма",				LOCATION_Modoc_5_1,		0, 151, 184 )
			.add( "Лесопилка",			LOCATION_Modoc_6_1,		0, 162, 154 )
			.add( "Вышка",				LOCATION_Modoc_1_2,		0, 175, 178 )
			.add( "Пустырь",			LOCATION_Modoc_2_2,		0, 172, 185 )
			.add( "Бурый лес",			LOCATION_Modoc_5_2,		0, 164, 184 )
			.add( "Серый лес",			LOCATION_Modoc_6_2,		0, 172, 185 )
			.add( "Торговый пост",		LOCATION_Modoc_1_3,		0, 178, 175 )
			.add( "Болото",				LOCATION_Modoc_2_3,		0, 182, 191 )
			.add( "Тропа в Модок",		LOCATION_Modoc_3_3,		0, 174, 182 )
			.add( "Дорога в Модок",		LOCATION_Modoc_4_3,		0, 193, 176 )
			.add( "Цветущие болота",	LOCATION_Modoc_5_3,		0, 174, 188 )
			.add( "Озеро",				LOCATION_Modoc_6_3,		0, 191, 196 )			
			.add( "Руины когтей",		LOCATION_Modoc_1_4,		0, 179, 194 )
			.add( "Логово когтей",		LOCATION_Modoc_1_4,		1, 190, 183 )
			.add( "Зона Альфа",			LOCATION_Modoc_2_4,		0, 191, 196 )
			.add( "Пустошь",			LOCATION_Modoc_3_4,		0, 171, 179 )
			.add( "Кафе Путник",		LOCATION_Modoc_4_4,		0, 154, 189 )
			.add( "Руины",				LOCATION_Modoc_5_4,		0, 185, 168 )
			.add( "Свалка",				LOCATION_Modoc_6_4,		0, 182, 158 )
			.add( "Феникс верх",		LOCATION_Modoc_1_5,		0, 162, 161 )
			.add( "Феникс вход",		LOCATION_Modoc_1_5,		1, 220, 184 )
			.add( "Феникс низ",			LOCATION_Modoc_1_5,		2, 177, 174 )
			.add( "Зона Бета",			LOCATION_Modoc_2_5,		0, 174, 178 )
			.add( "Окраина",			LOCATION_Modoc_3_5,		0, 182, 189 )
			.add( "Дорога в Саттер",	LOCATION_Modoc_4_5,		0, 177, 161 )
			.add( "Заправка",			LOCATION_Modoc_5_5,		0, 153, 132 )
			.add( "Лагерь Мутантов",	LOCATION_Modoc_6_5,		0, 193, 170 )
			.add( "Депо",				LOCATION_Modoc_4_6,		0, 179, 202 )
		)
		.add( Teleport( "Танкер" )
			.add( "Палуба",					LOCATION_Tanker,		0, 224, 241 )
			.add( "Торговая",				LOCATION_Tanker,		1, 195, 219 )
			.add( "Отдых",					LOCATION_Tanker,		2, 199, 216 )
			.add( "Промышленный",			LOCATION_Tanker,		3, 264, 196 )
			.add( "Дыра",					LOCATION_Tanker,		4, 294, 201 )
			.add( "Башня",					LOCATION_Tanker,		5, 180, 255 )
			.add( "Заседания",				LOCATION_Tanker,		6, 177, 242 )
			.add( "Барраки",				LOCATION_Tanker,		7, 180, 255 )
			.add( "Штаб",					LOCATION_Tanker,		8, 180, 255 )
			.add( "Мостик",					LOCATION_Tanker,		9, 180, 255 )
		)
	;
}


/*
			.add( Teleport( "Riverdale" )
				.add( "Riverdale",					35,		0, 474, 335 )
				.add( "Underground",				35,		1, 480, 370 )
				.add( "2nd floor",					35,		2, 185, 160 )
				.add( "3rd floor",					35,		3, 152, 150 )
				.add( "Mines",						40,		0, 170, 270 )
				.add( "Lead Mine",					41,		0, 170, 270 )
				)
			.add( Teleport( "Suburbs" )
				.add( "Road",						42,		0, 170, 270 )
				.add( "Fisherman Retreat",			48,		0, 170, 270 )
				.add( "Bridge",						38,		0, 170, 270 )
				.add( "Cliff",						39,		0, 170, 270 )
				.add( "Hills",						42,		0, 170, 270 )
				.add( "Beach",						43,		0, 170, 270 )
				)
			.add( Teleport( "Lone Pine" )
				.add( "Old Mine",					130,		0, 170, 270 )
				.add( "Ruins",						133,		0, 170, 270 )
				.add( "Cemetry",					135,		0, 170, 270 )
				.add( "Industrial",					176,		0, 170, 270 )
				.add( "Center",						178,		0, 170, 270 )
				.add( "Residential",				180,		0, 170, 270 )
				.add( "Blooming",					196,		0, 170, 270 )
				.add( "Blooming 2 floor",			197,		1, 205, 190 )
				)	
			.add( Teleport( "Canyon" )
				.add( "Canyon",						151,		0, 170, 270 )
				.add( "Outpost",					131,		0, 170, 270 )
				.add( "MT - West",					254,		0, 170, 270 )
				.add( "MT - East",					255,		0, 170, 270 )
				.add( "MT - Mine",					257,		1, 190, 210 )
				.add( "Uranium Mine",				258,		2, 165, 150 )
				.add( "Toxic Pit",					143,		0, 170, 270 )
				.add( "Canyon South",				252,		0, 170, 270 )
				.add( "Lair Of Death",				148,		0, 170, 270 )
				.add( "East Cliff",					134,		0, 170, 270 )
				.add( "Gorge",						146,		0, 170, 270 )
				.add( "West Cliff",					159,		0, 170, 270 )
								.add( "Devil's Island",				136,	0, 170, 270 )
				.add( "Foothills",					137,	0, 170, 270 )
				
				
				
				)
			.add( Teleport( "North Regions" )
				.add( "Swamp",						138,		0, 170, 270 )
				.add( "Old Church",					139,		0, 170, 270 )
				.add( "Forest Tribe",				130,		0, 170, 270 )
				.add( "Forest of Bones",			183,		0, 170, 270 )
				.add( "West Cliff",					159,		0, 170, 270 )
				.add( "Dogs Motel",					172,		0, 170, 270 )
				.add( "Abandoned Mine",				174,		0, 170, 270 )
				.add( "Tribal Burrial",				153,		0, 170, 270 )
				.add( "Crossroads",					155,		0, 170, 270 )
				.add( "Road to Shady",				157,		0, 170, 270 )
				)
			)
			*/

class Pos
{
	uint mapId;
	uint x;
	uint y;
	
	Pos( Map@ map, uint x, uint y )
	{
		this.mapId = valid( map ) ? map.Id : 0;
		this.x = x;
		this.y = y;
	}

	Pos( uint mapId, uint x, uint y )
	{
		this.mapId = mapId;
		this.x = x;
		this.y = y;
	}
	
	bool TransitToMap( Critter& cr )
	{
		if( mapId == 0 ) return false;
		
		Map@ map = GetMap( mapId );
		if( !valid( map ) ) return false;
		
		int attempt = 0;
		uint16 hexX = x, hexY = y;
		while( attempt < 10 )
		{
			if( !map.IsHexPassed( hexX, hexY ) )
			{
				map.MoveHexByDir( hexX, hexY, Random( 0, 5 ), 1 );
			}
			attempt++;
		}

		if( !map.IsHexPassed( hexX, hexY ) )
		{
			cr.Say( SAY_NETMSG, "|0xFFFF00 Hexes are busy." );
			return false;
		}
		
		cr.TransitToMap( mapId, hexX, hexY, cr.Dir );
		
		Location@ loc = map.GetLocation();
		if( valid(loc) )
		{
			cr.SetWorldPos( loc.WorldX, loc.WorldY );
		}
		else
		{
			cr.Say( SAY_NETMSG, "|0xFFFF00 You have no idea where you are." );
		}	
		return true;
	}
	
	uint online() 
	{
		if( mapId == 0 )
		{
			return 0;
		}
		
		Map@ map = GetMap( mapId );
		if( !valid( map ) )
		{
			return 0;
		}

		return CountMapPlayers( map );
	}
}

class Teleport
{
	Teleport[] _list;
	uint list() 
	{ 
		return _list.length(); 
	}
	
	Teleport@ get( uint n ) 
	{ 
		if( n < 0 || n >= list() ) 
		{
			Log( "[" + name() + "] " + "Attempt of getting teleport #" + n + "/" + list() + "." );
			return null; 
		}
		
		return _list[n];
	}
	
	string _name;
	string name() { return _name; }
	string name( uint n ) 
	{ 
		Teleport@ target = get(n);
		return valid( target ) ? target.name() : "N-A";
	}

	Teleport( string name )
	{
		init( name );
	}
	
	Teleport( string name, uint locPID, uint mapN, uint x, uint y )
	{
		init( name );
		
		Map@ map = GetLocationMap( locPID, mapN );
		@this._pos = Pos( map, x, y );
		
		if( !valid( map ) )
		{
			this._name += " #" + locPID + "-" + mapN;
		}
	}
	
	void init( string name )
	{
		this._name = name;
		this._list.resize(0);
		@this._pos = null;
	}
	
	Teleport@ add( string name )
	{
		return add( Teleport( name ) );
	}
	
	Teleport@ add( string name, uint locPID, uint mapN, uint x, uint y )
	{
		return add( Teleport( name, locPID, mapN, x, y ) );
	}
	
	Teleport@ add( Teleport teleport )
	{
		_list.insertLast( teleport );
		return this;
	}

	Pos@ _pos;
	Teleport@ pos( Pos@ pos )
	{
		@this._pos = @pos;
		return this;
	}

	Teleport@ pos( Map@ map, uint x, uint y )
	{
		return pos( Pos( map, x, y ) );
	}

	Teleport@ pos( uint mapId, uint x, uint y )
	{
		return pos( Pos( mapId, x, y ) );
	}

	uint posOnline() 
	{
		return valid(_pos) ? _pos.online() : 0;
	}

	uint online() 
	{ 
		uint result = posOnline();
		for( uint n = 0, l = list(); n < l; n++ )
		{
			result += online(n);
		}
		
		return result;
	}
	
	uint online( uint n ) 
	{ 
		Teleport@ target = get(n);
		return valid( target ) ? target.online() : 0;
	}

	bool warp( Critter& cr )
	{
		return _pos.TransitToMap( cr );
	}
}

void ShowPlayers( Critter& cr )
{
	OpenMenu( cr, "SelectPlayer", MenuPlayers() );
	cr.RunClientScript( "client_gui@SetPlayerMenuActive", 1, 0, 0, null, null );
	cr.StatBase[ST_VAR0] = -1;
}

void unsafe_stopPlayerMenu( Critter& player, int param0, int param1, int param2, string@ param3, int[] @ param4 )
{
	if( !isGM(player) )
	{
		return;
	}
	
	player.StatBase[ST_VAR0] = 0;
}

class MenuPlayers: CenteredMenuHandler
{
	bool MenuUpdate( Critter& cr, iDialogBox& menu )
	{
		if( cr.Stat[ST_VAR0] == 0 )
		{
			return false;
		}
		
		Critter@[] crs;
		uint count = GetAllPlayers( crs );
		for( uint n = 0; n < count; n++ )
		{
			string text;
			
			if( isGM( crs[n] ) )
			{
				text += "[GM]";
			}
			else if( crs[n].IsDead() )
			{
				text += "x_X";
			}
			else if( crs[n].IsKnockout() )
			{
				if( crs[n].Stat[ ST_CURRENT_HP ] > 0 )
				{
					text += "-_-";
				}
				else
				{
					text += "o_O";
				}
			}
			else
			{
				text += "^_^";
			}
			
			text += " " + GetPlayerName( crs[n].Id );
			
			if( menu.Button( text ) )
			{ 
				Map@ map = crs[n].GetMap();
				if( !valid( map ) )
				{
					cr.Say( SAY_NETMSG, "Цель на глобале, используйте '#cr2me " + crs[n].Id + "' что бы выдернуть её к себе." );
				}
				
				Location@ loc = map.GetLocation();
				cr.SetWorldPos(loc.WorldX, loc.WorldY);

				uint16 hexX, hexY;
				do
				{
					hexX = crs[n].HexX;
					hexY = crs[n].HexY;
					
					map.MoveHexByDir( hexX, hexY, Random( 0, 5 ), Random( 3, 5 ) );
				}
				while( !map.IsHexPassed( hexX, hexY ) );
				
				cr.TransitToMap( map.Id, hexX, hexY, 0 );
				return false;
			}
		}		
		
		return true;
	}

	int getDescriptionFile()
	{
		return TEXTMSG_TEXT;
	}

	int getDescriptionLine()
	{
		return STR_TELEPORTS_MENU_MAIN;
	}
	
	string@ Description( Critter& cr )
	{
		Map@ map = cr.GetMap();
		uint count = CountMapPlayers( map );
		
		Critter@[] crs;
		uint online = GetAllPlayers(crs);
		
		string info = "$name" + "\n" 
					+ "Онлайн " + online + " игроков." + "\n" 
					+ "На карте " + count + " игроков.";
		
		return info;
	}
	
	bool ShouldRedraw( Critter& cr )
	{
		return true;
	}	
}

#endif